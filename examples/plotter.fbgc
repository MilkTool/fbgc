pprint_tuple = fun(t)
	for(i = 0:|t|)
		print(t[i]);
	end
end

map = fun(input,i_low,i_high,o_low,o_high)
	return (input - i_low) * (o_high - o_low) / (i_high - i_low) + o_low
end

max = fun(x)
	if(type(x) == type( () ) )

		max_no = x[0]

		for(i = 1:|x|)
			if( x[i] > max_no)
				max_no = x[i]
			end		
		end

		return max_no
	end
end

min = fun(x)
	if(type(x) == type( () ) )

		min_no = x[0]

		for(i = 1:|x|)
			if( x[i] < min_no)
				min_no = x[i]
			end		
		end

		return min_no
	end
end


tuple_matris = fun(size_x,size_y)
	grid = ()

	for(i = 0:size_x)
		grid <<= ()
	end


	for(i = 0:|grid|)
		d = ()
		for(j = 0:size_y)
			d <<= 0
		end	
		grid[i] = d
	end

	for(i = 0:size_x)
		for(j = 0:size_y)
			grid[i,j] = 0
		end
	end

	return grid
end

my_linspace = fun(from, to, size)

	output = ()

	increment = (to-from)/(1.0 * (size - 1 ));
	
	for(i = 0:size)
		output <<= from + i * increment;
	end

	return output;
end

plot = fun( x_axis,y_axis,size)
	
	size_x = size;
	size_y = size;

:>we have 30x30 screen size to plot our data
:>Incoming data must be scaled to plot.
:>y axis character size is 30 but x axis is 60

	scaled_x = x_axis;
	scaled_y = y_axis;

	x_axis_min = min(x_axis)
	x_axis_max = max(x_axis)

	y_axis_min = min(y_axis)
	y_axis_max = max(y_axis)
		

	for(i = 0:|scaled_x|)
		scaled_x[i] = map(x_axis[i], x_axis_min, x_axis_max,0,size_x-1);
		scaled_y[i] = map(y_axis[i], y_axis_max, y_axis_min,0,size_y-1);
	end

	grid = tuple_matris(size_x,size_y)


	for(i = 0:|scaled_x|)
		grid[ int(scaled_y[i]), int(scaled_x[i]) ] = 1;
	end



	fl = fopen("window.txt","w+");		
	fl.fprint("This program is written in FBGC..\n");
	fl.fprint("Output : \n ");


	for(i = 0:|grid|)
		fl.fprint("--");
	end
	
	fl.fprint("\n")



	for(i = 0:|grid|)
		fl.fprint("|");
		for(j = 0:|grid[i]|)
			if( grid[i,j] == 1 )
				fl.fprint(" *"); 
			else
				fl.fprint("  ");
			end

		end

		fl.fprint("|");

		if(i == 0)
			fl.fprint( "%g" % y_axis_max );
		end


		fl.fprint("\n");
	end
	

	

	fl.fprint(" ");

	for(i = 0:|grid|)
		fl.fprint("--");
	end
	fl.fprint("\n")	


	:>printing x axis values
	fl.fprint(" %g" % x_axis_min );
	
	for(i = 0:size_x) 
		fl.fprint("  ");
	end

	fl.fprint(" %g" % x_axis_max );
	
	fl.fprint(" \n");

	fclose(fl);
	
end

square_wave = fun(x)
	y = ()
	n = 1.0;
	for(i = x)
		y <<= (1.0/n)*sin(n*i);
		n += 2.0;
	end
	return y;
end

gaussian = fun(x)
	y = ()
	for(i = x)
	y <<= exp(0-1.0*i**2);
	end
	return y;
end


sin_tuple = fun(x)
	y = ()
	for(i = x)
	y <<= sin(i);
	end
	return y;	
end
print("This is file plotter. It needs X and Y values of your\ndata to print as a plot in your file.\n\n\n");

user_input = 0;

:>x = (0,1,2,3,4,5,6,7)
:>y = (0,1,4,9,16,25,36,49)
pi = 3.14159265359;
x = my_linspace(0,2*pi,300);
y = sin(x);

plot(x,y,40);